address_list = { ( group | contact_list ) }

WS = _{ " " | "\t" }
NL = _{ "\n" | "\r" }

garbage = @{ garbage_inside* }

garbage_nongreedy = @{ garbage_inside+ }

garbage_inside = { (!(NL | ",") ~ ANY) }

contact_list = {
    ("," | ";,")? ~ contact ~ (WS* ~ ("," | ";,") ~ WS* ~ contact)* ~ ("," | ";,")?
}

group = {
    name ~ WS* ~ ":" ~ WS* ~ ";" ~ contact_list |
    name ~ WS* ~ ":" ~ WS* ~ ";" |
    name ~ WS* ~ ":" ~ WS* ~ "<" ~ WS* ~ ">" ~ WS* ~ ";" |
    name ~ WS* ~ ":" ~ WS* ~ contact_list ~ WS* ~ ";" |
    "<" ~ name ~ WS* ~ ":" ~ WS* ~ "<"? ~ WS* ~ ">"? ~ WS* ~ ";" ~ WS* ~ ">" |
    "<" ~ name ~ WS* ~ ":" ~ WS* ~ contact_list ~ WS* ~ ";" ~ WS* ~ ">"
}

name = {
    WS* ~ "\"" ~ escaped ~ "\"" |
    WS* ~ "'" ~ escaped_single ~ "'" |
    WS* ~ clear
}

email_angle = {
    WS* ~ "<" ~ WS* ~ "'" ~ email ~ ("," | "." | ";")? ~ "'" ~ WS* ~ ">" ~ WS* |
    WS* ~ "<" ~ WS* ~ email ~ ("," | "." | ";")? ~ WS* ~ ">" ~ WS*
}

mailbox_angle = {
    WS* ~ "<" ~ WS* ~ mailbox ~ ("," | "." | ";")? ~ WS* ~ ">" ~ WS*
}

malformed_comment_name = @{
    comment
}

contact = {
    email ~ email_angle ~ "(" ~ comment ~ ")" |
    email ~ WS* ~ "(" ~ comment ~ ")" ~ WS* ~ email_angle |
    email ~ email_angle |
    email ~ name ~ email_angle |
    email ~ WS* ~ "(" ~ name ~ ")" |
    email ~ WS* ~ "(" ~ malformed_comment_name ~ ")" |
    email ~ ">" |
    email ~ (".")? |
    name ~ WS* ~ "<" ~ WS* ~ ">" ~ WS* ~ email |
    email_angle ~ ">" |
    email_angle ~ "(" ~ malformed_comment_name ~ ")" |
    email_angle |
    name ~ email_angle ~ "(" ~ comment ~ ")" |
    name ~ email_angle ~ ">" |
    name ~ email_angle |
    name ~ "(" ~ comment ~ ")" ~ email_angle |
    "\"" ~ email ~ "\"" |
    (" " | "<" | ">" | ",")* ~ email |
    malformed ~ email_angle ~ ">" |
    malformed ~ email_angle |
    name ~ WS* ~ mailbox_angle |
    malformed ~ WS* ~ mailbox_angle |
    mailbox ~ WS* ~ "(" ~ name ~ ")" |
    mailbox ~ garbage_nongreedy |
    mailbox ~ (".")? |
    name ~ "<" ~ email |
    malformed ~ "<" ~ email |
    garbage
}

email = @{ local+ ~ "@" ~ domain+ ~ ("." ~ domain+)+ }

mailbox = @{ local+ ~ "@" ~ domain+ }

local = {
    "\"" ~ escaped ~ "\"" |
    (!("@" | "<" | ">" | ":" | "," | "'" | WS) ~ ANY)+
}

// TODO make sure we implement https://tools.ietf.org/html/rfc5894#page-9
domain = {
    !("@" | "\\" | "[" | "]" | "<" | ">" | "(" | ")" | "$" | "#" | "%" | "^" |
      "," | "." | "\"" | "'" | ";" | "*" | "{" | "}" | "&" | "=" | "?" | "~" |
      WS | NL ) ~ ANY
}

escaped = @{
    (!("\\" | "\"" | NL) ~ ANY |
     "\\" ~ ANY)*
}

escaped_single = @{
    (!("\\" | "'" | NL) ~ ANY |
     "\\" ~ ANY)*
}

clear = @{
    (!("@" | ":" | "<" | ">" | "\"" | "(" | ")" | "," | "[" | "]") ~ ANY)+
}

malformed = @{
    (clear | "(" | ")" | "[" | "]" | "\"" | ":" | "," | "@")+
}

comment = @{
    "\"" ~ escaped ~ "\"" ~ comment_inside |
    comment_inside ~ "\"" ~ escaped ~ "\"" ~ comment_inside |
    comment_inside ~ "\"" ~ escaped ~ "\"" |
    comment_inside |
    (!("(" | ")" | NL) ~ ANY)+
}

comment_inside = {
    (!("(" | ")" | "\"" | NL) ~ ANY)+
}

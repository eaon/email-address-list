address_list = { ( group | contact_list ) }

WS = _{ " " | "\t" }
NL = _{ "\n" | "\r" }

garbage = @{ (!(NL | ",") ~ ANY)* }

contact_list = {
    ","? ~ contact ~ (WS* ~ "," ~ WS* ~ contact)* ~ ","?
}

group = {
    name ~ WS* ~ ":" ~ WS* ~ ";" |
    name ~ WS* ~ ":" ~ WS* ~ "<" ~ WS* ~ ">" ~ WS* ~ ";" |
    name ~ WS* ~ ":" ~ WS* ~ contact_list ~ WS* ~ ";" |
    "<" ~ name ~ WS* ~ ":" ~ WS* ~ "<"? ~ WS* ~ ">"? ~ WS* ~ ";" ~ WS* ~ ">" |
    "<" ~ name ~ WS* ~ ":" ~ WS* ~ contact_list ~ WS* ~ ";" ~ WS* ~ ">"
}

name = {
    WS* ~ "\"" ~ escaped ~ "\"" |
    WS* ~ clear
}

email_angle = {
    WS* ~ "<" ~ email ~ ","? ~ ">" ~ WS*
}

mailbox_angle = {
    WS* ~ "<" ~ mailbox ~ ","? ~ ">" ~ WS*
}

contact = {
    email |
    email_angle |
    name ~ email_angle |
    name ~ WS* ~ "(" ~ comment* ~ ")" ~ WS* ~ email_angle |
    "\"" ~ email ~ "\"" |
    (" " | "<" | ">" | ",")* ~ email |
    malformed ~ email_angle |
    name ~ WS* ~ mailbox_angle |
    malformed ~ WS* ~ mailbox_angle |
    mailbox |
    garbage
 }

email = @{ local+ ~ "@" ~ domain+ ~ ("." ~ domain+)+ }

mailbox = @{ local+ ~ "@" ~ domain+ }

local = {
    "\"" ~ escaped ~ "\"" |
    (!("@" | "<" | ">" | ":" | "," | WS) ~ ANY)+
}

// XXX make sure we implement https://tools.ietf.org/html/rfc5894#page-9
domain = {
    !("@" | "\\" | "[" | "]" | "<" | ">" | "(" | ")" | "$" | "#" | "%" | "^" |
      "," | "." | "\"" | "'" | ";" | "*" | "{" | "}" | "&" | "=" | "?" | "~" |
      WS | NL ) ~ ANY
}

escaped = @{
    (!("\\" | "\"" | NL) ~ ANY |
     "\\" ~ ANY)*
}

clear = @{
    (!("@" | ":" | "<" | ">" | "\"" | "(" | ")" | "," | "[" | "]") ~ ANY)+
}

malformed = @{
    (clear | "(" | ")" | "[" | "]" | "\"" | ":" | "," | "@")+
}

comment = @{
    (!("(" | ")" | NL) ~ ANY)+
}

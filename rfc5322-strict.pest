address_list =  { (address ~ ("," ~ address)*) | obs_addr_list }

obs_addr_list = { (CFWS? ~ ",")* ~ address ~ ("," ~ (address | CFWS)?)* }

address = { mailbox | group }

mailbox = { name_addr | addr_spec }

group = { display_name ~ ":" ~ group_list? ~ ";" ~ CFWS? }

name_addr = { display_name? ~ angle_addr }

addr_spec = @{ local_part ~ "@" ~ domain }

display_name = @{ phrase }

group_list = { mailbox_list | CFWS | obs_group_list }

CFWS = { ((FWS? ~ comment)+ ~ FWS?) | FWS }

angle_addr = @{ CFWS? ~ "<" ~ addr_spec ~ ">" ~ CFWS? | obs_angle_addr }

local_part =  { dot_atom | quoted_string }

domain = { dot_atom | domain_literal }

phrase = { word+ | obs_phrase }

mailbox_list = { (mailbox ~ ("," ~ mailbox)*) | obs_mbox_list }

obs_group_list = { (CFWS? ~ ",")+ ~ CFWS? }

FWS = { ((WSP* ~ CRLF)+ ~ WSP+) | obs_FWS }

comment = { "(" ~ (FWS? ~ ccontent)* ~ FWS? ~ ")" }

obs_angle_addr = { CFWS? ~ "<" ~ obs_route ~ addr_spec ~ ">" ~ CFWS? }

dot_atom = { CFWS? ~ dot_atom_text ~ CFWS? }

quoted_string = { CFWS? ~ DQUOTE ~ (FWS? ~ qcontent)* ~ FWS? ~ DQUOTE ~ CFWS? }

domain_literal = { CFWS? ~ "[" ~ (FWS? ~ dtext)* ~ FWS? ~ "]" ~ CFWS? }

word = { atom | quoted_string }

obs_phrase = { word ~ (word | "." | CFWS)* }

obs_mbox_list = { (CFWS? ~ ",")* ~ mailbox ~ ("," ~ (mailbox | CFWS)?)* }

WSP = { " " | "\t" }

CRLF = { "\r" ~ "\n" }

obs_FWS =  { WSP+ ~ (CRLF ~ WSP+)* }

ccontent = { ctext | quoted_pair | comment }

obs_route = { obs_domain_list ~ ":" }

dot_atom_text = { atext+ ~ ("." ~ atext+)* }

DQUOTE = { "\"" }

qcontent = { qtext | quoted_pair }

dtext = { !("[" | "]" | "\\") ~ ASCII | obs_dtext }

atom = @{ CFWS? ~ atext+ ~ CFWS? }

ctext = { !("(" | ")" | "\\") ~ ASCII | obs_ctext }

quoted_pair = { ("\\" ~ (VCHAR | WSP)) | obs_qp }

obs_domain_list = { (CFWS | ",")* ~ "@" ~ domain ~ ("," ~ CFWS? ~ ("@" ~ domain)?) }

atext = { ASCII_ALPHA | ASCII_DIGIT | "!" | "#" | "$" | "%" | "&" | "'" | "*" | "+" | "-" | "/" | "=" | "?" | "^" | "_" | "`" | "{" | "|" | "}" | "~" }

qtext = { !("\\" | "\"" ) ~ ASCII | obs_qtext }

obs_dtext = { obs_NO_WS_CTL | quoted_pair }

obs_ctext = { obs_NO_WS_CTL }

VCHAR = { !(" ") ~ ASCII }

obs_qp = { "\\" ~ (" " | obs_NO_WS_CTL | "\n" | "\r") }

obs_qtext = { obs_NO_WS_CTL }

obs_NO_WS_CTL = { !(" " | "\r" | "\n" | "\t") ~ ASCII }
